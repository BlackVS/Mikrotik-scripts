##########################################################################
# func_cmp2dates - compare two dates
#  Input: date1, date2 in format "mmm/dd/yyyy". For example "jan/01/1970"
#  Output: 
#    -1 if date1<date2  
#     0 if date1=date2
#     1 if date1>date2
##########################################################################
# uncomment for testing
#:local date1 "nov/10/2016"
#:local date2 [/system clock get date]
#:put ($date1." compared to ".$date2)
########################################

#compare years
:local year1 [:tonum [:pick $date1 7 11]]
:local year2 [:tonum [:pick $date2 7 11]]
if ($year1<$year2) do={ :return -1 }
if ($year1>$year2) do={ :return  1 }

#compare months
:local months {"jan"=1;"feb"=2;"mar"=3;"apr"=4;"may"=5;"jun"=6;"jul"=7;"aug"=8;"sep"=9;"oct"=10;"nov"=11;"dec"=12}
:local month1 ($months->[:pick $date1 0 3])
:local month2 ($months->[:pick $date2 0 3])
if ($month1<$month2) do={ :return -1 }
if ($month1>$month2) do={ :return  1 }

#compare days
:local day1 [:tonum [:pick $date1 4 6]]
:local day2 [:tonum [:pick $date2 4 6]]
if ($day1<$day2) do={ :return -1 }
if ($day1>$day2) do={ :return  1 }
:return 0